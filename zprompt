# Configure the ZSH prompt.

autoload -Uz vcs_info

function theme_precmd {
    vcs_info

    local TERMWIDTH
    (( TERMWIDTH = ${COLUMNS} - 1 ))


    ###
    # Truncate the path if it's too long.

    PR_FILLBAR=""
    PR_PWDLEN=""

    local promptsize=${#${(%):---( %n@%m )---()--}}
    local pwdsize=10

    if [[ "$promptsize + $pwdsize" -gt $TERMWIDTH ]]; then
      ((PR_PWDLEN=$TERMWIDTH - $promptsize))
    else
      PR_FILLBAR="\${(l.(($TERMWIDTH - ($promptsize + $pwdsize)))..${PR_HBAR}.)}"
    fi
}


theme_preexec () {
    if [[ "$TERM" == "screen" ]]; then
        local CMD=${1[(wr)^(*=*|sudo|-*)]}
        echo -n "\ek$CMD\e\\"
    fi
}


setprompt () {
    ###
    # Need this so the prompt will work.

    setopt prompt_subst


    ###
    # See if we can use colors.

    autoload colors zsh/terminfo
    if [[ "$terminfo[colors]" -ge 8 ]]; then
        colors
    fi
    for color in RED GREEN YELLOW BLUE MAGENTA CYAN WHITE GREY; do
        eval PR_$color='%{$terminfo[bold]$fg[${(L)color}]%}'
        eval PR_LIGHT_$color='%{$fg[${(L)color}]%}'
        (( count = $count + 1 ))
    done
    PR_NO_COLOUR="%{$terminfo[sgr0]%}"

    typeset -A altchar
    set -A altchar ${(s..)terminfo[acsc]}
    PR_SET_CHARSET="%{$terminfo[enacs]%}"
    PR_SHIFT_IN="%{$terminfo[smacs]%}"
    PR_SHIFT_OUT="%{$terminfo[rmacs]%}"
    PR_HBAR=${altchar[q]:--}
    PR_ULCORNER=${altchar[l]:--}
    PR_LLCORNER=${altchar[m]:--}
    PR_LRCORNER=${altchar[j]:--}
    PR_URCORNER=${altchar[k]:--}


    ###
    # Decide if we need to set titlebar text.

    case $TERM in
    xterm*)
        PR_TITLEBAR=$'%{\e]0;%(!.-=*[ROOT]*=- | .)%n@%m:%~ | ${COLUMNS}x${LINES} | %y\a%}'
        ;;
    screen)
        PR_TITLEBAR=$'%{\e_screen \005 (\005t) | %(!.-=[ROOT]=- | .)%n@%m:%~ | ${COLUMNS}x${LINES} | %y\e\\%}'
        ;;
    *)
        PR_TITLEBAR=''
        ;;
    esac


    ###
    # Decide whether to set a screen title
    if [[ "$TERM" == "screen" ]]; then
        PR_STITLE=$'%{\ekzsh\e\\%}'
    else
        PR_STITLE=''
    fi


    ###
    # Finally, the prompt.

    PROMPT='$PR_SET_CHARSET$PR_STITLE${(e)PR_TITLEBAR}\
$PR_CYAN$PR_SHIFT_IN$PR_ULCORNER$PR_HBAR$PR_BLUE$PR_SHIFT_OUT(\
$PR_GREEN%(!.$PR_RED.)%S %n@%m %s$PR_GREEN\
$PR_BLUE)$PR_SHIFT_IN$PR_CYAN$PR_HBAR$PR_HBAR${(e)PR_FILLBAR}$PR_HBAR$PR_BLUE$PR_SHIFT_OUT(\
$PR_YELLOW%$PR_PWDLEN<...<%D{%H:%M:%S}%<<\
$PR_BLUE)$PR_SHIFT_IN$PR_CYAN$PR_HBAR$PR_SHIFT_OUT»\

$PR_CYAN$PR_SHIFT_IN$PR_LLCORNER$PR_HBAR$PR_BLUE$PR_SHIFT_OUT(\
%(?..$PR_LIGHT_RED%?$PR_BLUE!)\
$PR_CYAN%!$PR_BLUE: $PR_YELLOW%c${vcs_info_msg_0_} \
%(!.$PR_RED.$PR_GREEN)%#$PR_BLUE)$PR_SHIFT_IN$PR_CYAN$PR_HBAR$PR_SHIFT_OUT»\
$PR_NO_COLOUR '

    RPROMPT='$(virtualenv_prompt_info)$PR_NO_COLOUR'

    PS2='$PR_CYAN$PR_SHIFT_IN$PR_HBAR$PR_SHIFT_OUT\
$PR_BLUE$PR_SHIFT_IN$PR_HBAR$PR_SHIFT_OUT(\
$PR_LIGHT_GREEN%_$PR_BLUE)$PR_SHIFT_IN$PR_HBAR$PR_SHIFT_OUT\
$PR_CYAN$PR_SHIFT_IN$PR_HBAR$PR_SHIFT_OUT$PR_NO_COLOUR '
}

setprompt

zstyle ':vcs_info:*' actionformats \
    " ${PR_MAGENTA}[%r${PR_LIGHT_BLUE}%b${PR_MAGENTA}|${PR_RED}%a${PR_MAGENTA}]"

zstyle ':vcs_info:*' formats       \
    " ${PR_MAGENTA}[${PR_LIGHT_BLUE}%b${PR_MAGENTA}]"

autoload -U add-zsh-hook
add-zsh-hook precmd  theme_precmd
add-zsh-hook preexec theme_preexec

# vim: set ft=sh
